#!/usr/bin/perl -CA
BEGIN {
    *arg = @ARGV ? \&CORE::shift : \&CORE::readline;
}
use strict;
use warnings;
use utf8;
use open qw( :encoding(UTF-8) :std );

use Unicode::Normalize;

while (defined ($_ = arg)) {
    chomp;
    $_ = NFD($_) =~ s/\p{Mn}//rg; # strip tashkeel

    # handle special cases
    s/ك\b/لـ/g;
    s/[نڹڻڼڽ](?!\b)/ٮ/g;

    tr # strip dots in arabic
     {بتث جخ ذ ز ش ض ظ غ ف ق ن ة ي }
     {ٮٮٮ حح د ر س ص ط ع ڡ ٯ ں ه ى };

    tr # strip in other langs
     {ٹٺٻټٽپٿڀ ځڂڃڿڄڅچڇ ڈډڊڋڌڍڎڏڐ ڑڒړڔڕږڗژڙ ښڛڜ ڝڞ ڟ ڠ ڡڢڣڤڥڦ ڧڨ ػؼ ڵڶڷڸ ڹڻڼڽ ۀہۂۃ ۄۅۆۇۏۈۉۊۋ ۍؽؾؿێېۑ ۓ}
     {ٮٮٮٮٮٮٮٮ حححححححح ددددددددد ررررررررر سسس صص ط ع ڡڡڡڡڡڡ ٯٯ كك لللل ںںںں هههه ووووووووو ىىىىىىى ے};

    print $_, "\n";
}


